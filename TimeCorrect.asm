/*
Основные процедуры для корректировки времени часов

1. Корректировка времени часов. В связи с тем, что в процессе работы часов в течении определенного времени, из-за неидеальной точности работы кварцевого 
резонатора, возникает погрешность определения времени микросхемой DS1307. Поэтому каждые сутки, в определённое время, происходит корректировка часов. Для этого
нужно самостоятельно определить значение ухода времени в секундах за сутки для каждой конкретной модели часов и записать во флэш-память.

2. Считывание из памяти программ поправочного коэффициента (уход времени в секундах за сутки) и запись полученного значения в оперативную
память. Производится сразу после включения часов и начала работы программы.

3. Считывание из оперативной памяти значения поправки (уход времени в секундах за сутки). Результат - в регистре temph.
*/

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////							1
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Correct_time:

	//считывание текущего времени - сначала часы
	ldi		XL,LOW(TWI_BUFFER_IN)
	ldi		XH,HIGH(TWI_BUFFER_IN)
	adiw	XL,2
	ld		templ,X
	//проверка значения часов, минут и секунд (корректировка производится ежедневно в 01:00:00)
	cpi		templ, 1
	brne	exit_correct_time_proc
	ld		templ,-X													;минуты
	cpi		templ,0
	brne	exit_correct_time_proc
	ld		templ,-X													;секунды
	cpi		templ,0
	brne	check_stop_correction_time	
	call	Read_time_inaccuracy_from_RAM
	//проверка значения ошибки часов - если значение отрицательное (старший бит = 1) - отстают, иначе - спешат
	sbrs	temph,7														
	rjmp	clock_ahead
	//если отстают - записать в датчик значение секунд и выйти
	neg		temph														;модуль отрицательного числа
	call	HEX_TO_BCD													;перевести в формат bcd для записи в датчик
	clr		byte_address
	call	Write_byte_to_DS1307
	rjmp	exit_correct_time_proc

	//если часы спешат
clock_ahead:
	sbr		state_flag,(1<<WWSM)										;установка флага, который сигнализирует, что будет производится коррекция времени
	rjmp	exit_correct_time_proc

//для случая, если часы спешат корректировка времени будет происходить в 1 час 00 минут, а значение секунд = значение поправки + 1
//будет произведён сброс до значения = 1:00:01 и сброс флага WWSM
check_stop_correction_time:
	sbrs	state_flag,WWSM
	rjmp	exit_correct_time_proc
	mov		temph,templ
	call	BCD_TO_HEX
	mov		templ,temph													;перевод значения секунд из BCD формата
	call	Read_time_inaccuracy_from_RAM								;значение поправки - в temph
	inc		temph
	cp		temph,templ													;проверка на равенство текущего значения секунд и значения поправки + 1
	brne	exit_correct_time_proc
	cbr		state_flag,(1<<WWSM)										;сброс флага
	ldi		temph,1
	clr		byte_address
	call	Write_byte_to_DS1307										;запись в датчик значения секунд = 1

exit_correct_time_proc:
ret

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////							2
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Read_write_time_inaccuracy:	
	ldi		ZL,LOW(2*TIME_INACCURACY_SEC_PER_DAY_CONST)					;адрес хранения значения ухода времени во флеш-памяти
	ldi		ZH,HIGH(2*TIME_INACCURACY_SEC_PER_DAY_CONST)
	lpm		templ,Z
	
	ldi		XL,LOW(TIME_INACCURACY_SEC_PER_DAY_TEMP)					;адрес хранения значения ухода времени в оперативной памяти
	ldi		XH,HIGH(TIME_INACCURACY_SEC_PER_DAY_TEMP)
	st		X,templ
ret

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////							3
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Read_time_inaccuracy_from_RAM:
	ldi		XL,LOW(TIME_INACCURACY_SEC_PER_DAY_TEMP)					;адрес хранения значения ухода времени в оперативной памяти
	ldi		XH,HIGH(TIME_INACCURACY_SEC_PER_DAY_TEMP)
	ld		temph,X
ret

.dseg 

TIME_INACCURACY_SEC_PER_DAY_TEMP:	.byte	1

.cseg

TIME_INACCURACY_SEC_PER_DAY_CONST:
		.db		0x06,	0x00
